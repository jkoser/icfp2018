#lang racket

(require "coords.rkt")
(require "model.rkt")
(require "state.rkt")
(require "trace.rkt")

(provide run-system!
         step-system!)


;; Helper for bot-cmd-groups
(define (fusions-finder b1 nd1)
  (match-lambda
    ((list b2 'fusions nd2)
     (and (equal? (bot-pos b1) (c+ (bot-pos b2) nd2))
          (equal? (bot-pos b2) (c+ (bot-pos b1) nd1))))
    (else #f)))

;; Helper for bot-cmd-groups
(define (gfill-gvoid-finder op region)
  (match-lambda
    ((list b2 op2 nd2 fd2)
     #:when (equal? op op2)
     (let* ((corner (c+ (bot-pos b2) nd2))
            (region2 (make-region corner (c+ corner fd2))))
       (equal? region region2)))
    (else #f)))

;; (Bot Command Args ...)* -> ((Bot ...) Command Args ...)*
(define (bot-cmd-groups bcs)
  ;; Here groups is the result accumulator, pending is the unprocessed input,
  ;; and secondary holds FusionS commands we encounter before FusionP.
  (define (loop groups pending secondary)
    (if (empty? pending)
      (if (empty? secondary)
        groups
        (error "unmatched" secondary))
      (match (first pending)
        ((list b1 'fusionp nd1)
         (let-values (((matches others)
                       (partition (fusions-finder b1 nd1)
                                  (append (rest pending) secondary))))
           (if (= (length matches) 1)
             (loop (cons (list (list b1 (first (first matches)))
                               'fusionp nd1)
                         groups)
                   others
                   '())
             (error "unmatched" (first pending)))))
        ((list b1 'fusions nd1)
         (loop groups
               (rest pending)
               (cons (first pending) secondary)))
        ((list b1 op nd1 fd1)
         #:when (member op '(gfill gvoid))
         (let*-values (((corner) (c+ (bot-pos b1) nd1))
                       ((region) (make-region corner (c+ corner fd1)))
                       ((matches others)
                        (partition (gfill-gvoid-finder op region) pending)))
           (if (= (length matches) (expt 2 (dim region)))
             (loop (cons (list (map first matches)
                               op nd1 fd1)
                         groups)
                   others
                   secondary)
             (error "wrong number of bots" op))))
        ((list b cmd ...)
         (loop (cons (cons (list b) cmd)
                     groups)
               (rest pending)
               secondary)))))
  (loop '() bcs '()))


(define (execute-cmd-group! g s)
  (match g
    ((list (list b) 'halt)
     (set-system-bots! s '()))
    ((list (list b) 'wait)
     (void))
    ((list (list b) 'flip)
     (if (equal? (system-harmonics s) 'high)
       (set-system-harmonics! s 'low)
       (set-system-harmonics! s 'high)))
    ((list (list b) 'smove lld)
     (let ((c-prime (c+ (bot-pos b) lld)))
       (set-bot-pos! b c-prime)
       (system-add-energy! s (* 2 (mlen lld)))))
    ((list (list b) 'lmove sld1 sld2)
     (let* ((c-prime (c+ (bot-pos b) sld1))
            (c-prime2 (c+ c-prime sld2)))
       (set-bot-pos! b c-prime2)
       (system-add-energy! s (* 2 (+ (mlen sld1) 2 (mlen sld2))))))
    ((list (list b) 'fission nd m)
     (let* ((c-prime (c+ (bot-pos b) nd))
            (b2 (bot-fission! b c-prime m)))
       (set-system-bots! s (cons b2 (system-bots s)))
       (system-add-energy! s 24)))
    ((list (list b) 'fill nd)
     (let ((c-prime (c+ (bot-pos b) nd)))
       (if (model-voxel-full? (system-model s)
                              (x c-prime) (y c-prime) (z c-prime))
         (system-add-energy! s 6)
         (begin
           (model-voxel-fill! (system-model s)
                              (x c-prime) (y c-prime) (z c-prime))
           (system-add-energy! s 12)))))
    ((list (list b) 'void nd)
     (let ((c-prime (c+ (bot-pos b) nd)))
       (if (model-voxel-full? (system-model s)
                              (x c-prime) (y c-prime) (z c-prime))
         (begin
           (model-voxel-void! (system-model s)
                              (x c-prime) (y c-prime) (z c-prime))
           (system-add-energy! s -12))
         (system-add-energy! s 3))))
    ((list (list bp bs) 'fusionp nd)
     (set-system-bots! s (filter (lambda (b) (not (eq? b bs)))
                                 (system-bots s)))
     (bot-fusion! bp bs)
     (system-add-energy! s -24))
    ((list bots 'gfill nd1 fd1)
     (let* ((m (system-model s))
            (corner (c+ (bot-pos (first bots)) nd1))
            (region (make-region corner (c+ corner fd1))))
       (for* ((i (in-range (xmin region) (+ (xmax region) 1)))
              (j (in-range (ymin region) (+ (ymax region) 1)))
              (k (in-range (zmin region) (+ (zmax region) 1))))
         (if (model-voxel-full? m i j k)
           (system-add-energy! s 6)
           (begin
             (model-voxel-fill! s i j k)
             (system-add-energy! s 12))))))
    ((list bots 'gvoid nd1 fd1)
     (let* ((m (system-model s))
            (corner (c+ (bot-pos (first bots)) nd1))
            (region (make-region corner (c+ corner fd1))))
       (for* ((i (in-range (xmin region) (+ (xmax region) 1)))
              (j (in-range (ymin region) (+ (ymax region) 1)))
              (k (in-range (zmin region) (+ (zmax region) 1))))
         (if (model-voxel-full? m i j k)
           (begin
             (model-voxel-void! s i j k)
             (system-add-energy! s -12))
           (system-add-energy! s 3)))))))


(define (step-system! s)
  (define bots (sort (system-bots s)
                     <
                     #:key bot-bid))
  (define n (length bots))
  (when (< (length (system-trace s)) n)
      (error "not enough commands"))
  (define bot-cmds (map cons bots (take (system-trace s) n)))
  (define cmd-groups (bot-cmd-groups bot-cmds))
  (define r (model-res (system-model s)))
  (if (equal? (system-harmonics s) 'high)
    (system-add-energy! s (* 30 r r r))
    (system-add-energy! s (* 3 r r r)))
  (system-add-energy! s (* 20 n))
  (for ((g cmd-groups))
    (execute-cmd-group! g s))
  (set-system-trace! s (drop (system-trace s) n))
  (set-system-time! s (+ (system-time s) 1)))


(define (run-system! s)
  (do ()
    ((empty? (system-bots s)))
    (step-system! s)))
